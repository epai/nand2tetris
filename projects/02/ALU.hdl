// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
   // Put you code here:
        // zero x and/or y?
        And16(a=x, b=true, out=x1);
        And16(a=x, b=false, out=x2);
        Mux16(a=x1, b=x2, sel=zx, out=x3);
        And16(a=y, b=true, out=y1);
        And16(a=y, b=false, out=y2);
        Mux16(a=y1, b=y2, sel=zy, out=y3);
        //negate x and/or y?
        Not16(in=x3, out=nx1);
        Mux16(a=x3, b=nx1, sel=nx, out=x4);
        Not16(in=y3, out=ny1);
        Mux16(a=y3, b=ny1, sel=ny, out=y4);
        //choose between AND and ADD
        And16(a=x4, b=y4, out=r1);
        Add16(a=x4, b=y4, out=r2);
        Mux16(a=r1, b=r2, sel=f, out=out1);
        //negate the output?
        Not16(in=out1, out=out2);
        Mux16(a=out1, b=out2, sel=no, out[0..7]=out3, 
              out[8..14]=out4, out[15]=out5, out=out);
        //is output zero?
        Or8Way(in=out3, out=w1);
        Or8Way(in[0..6]=out4, in[7]=out5, out=w2);
        Or(a=w1, b=w2, out=w3);
        Not(in=w3, out=zr);
        //is output negative?
        And(a=out5, b=true, out=ng);
}